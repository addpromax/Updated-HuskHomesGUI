import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'org.ajoberstar.grgit' version '5.2.2'
}

group 'pro.obydux'
version "$ext.plugin_version"
description "$ext.plugin_description"
defaultTasks 'licenseFormat', 'build'

ext {
    set 'version', version.toString()
    set 'description', description.toString()
}

repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.minebench.de/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url 'https://repo.william278.net/releases' }
    maven { url 'https://repo.william278.net/snapshots' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:24.1.0'
    compileOnly 'net.william278.huskhomes:huskhomes-bukkit:4.7'

    implementation 'de.themoep:inventorygui:1.6.3-SNAPSHOT'
    implementation 'org.apache.commons:commons-text:1.12.0'
    implementation 'net.william278:minedown:1.8.2'
    implementation 'dev.dejvokep:boosted-yaml:1.3.7'
    implementation 'net.wesjd:anvilgui:1.10.2-SNAPSHOT'
    implementation 'net.william278:annotaml:2.0.7-4f14c61'
    implementation 'net.william278:desertwell:2.0.4'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
}

compileJava.options.encoding = 'UTF-8'
compileJava.options.release.set 17

processResources {
    filesNotMatching(['**/*.png', '**/*.svg']) {
        filter ReplaceTokens as Class, beginToken: '${', endToken: '}',
                tokens: rootProject.ext.properties
    }
}

shadowJar {
    relocate 'org.apache.commons.io', 'pro.obydux.huskhomes.gui.libraries.commons.io'
    relocate 'org.apache.commons.text', 'pro.obydux.huskhomes.gui.libraries.commons.text'
    relocate 'org.apache.commons.lang3', 'pro.obydux.huskhomes.gui.libraries.commons.lang3'
    relocate 'org.jetbrains', 'pro.obydux.huskhomes.gui.libraries'
    relocate 'org.intellij', 'pro.obydux.huskhomes.gui.libraries'
    relocate 'de.themoep.inventorygui', 'pro.obydux.huskhomes.gui.libraries.inventorygui'
    relocate 'de.themoep.minedown', 'pro.obydux.huskhomes.gui.libraries.minedown'
    relocate 'net.wesjd', 'pro.obydux.huskhomes.gui.libraries'
    relocate 'net.william278.annotaml', 'pro.obydux.huskhomes.gui.libraries.annotaml'
    relocate 'net.william278.desertwell', 'pro.obydux.huskhomes.gui.libraries.desertwell'
    relocate 'dev.dejvokep', 'pro.obydux.huskhomes.gui.libraries.boostedyaml'

    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')
}

jar {
    dependsOn('shadowJar')
}

test {
    useJUnitPlatform()
}

license {
    header = rootProject.file('HEADER')
    include '**/*.java'
    newLine = true
}

logger.lifecycle("Building HuskHomesGUI ${version} by William278 & Obydux")

@SuppressWarnings('GrMethodMayBeStatic')
def versionMetadata() {
    // Get if there is a tag for this commit
    def tag = grgit.tag.list().find { it.commit.id == grgit.head().id }
    if (tag != null) {
        return ''
    }

    // Otherwise, get the last commit hash and if it's a clean head
    if (grgit == null) {
        return '-' + System.getenv("GITHUB_RUN_NUMBER") ? 'build.' + System.getenv("GITHUB_RUN_NUMBER") : 'unknown'
    }
    return '-' + grgit.head().abbreviatedId + (grgit.status().clean ? '' : '-indev')
}
